plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
    id 'com.google.protobuf' version '0.8.8'
}

group 'com.igdb'
version '0.6'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    // HTTP
    implementation 'com.github.kittinunf.fuel:fuel:2.1.0'
    implementation 'com.github.kittinunf.fuel:fuel-gson:2.0.1'
    // Protocoll Buffers
    api 'com.google.protobuf:protobuf-java:3.8.0'
    // Tests
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    testRuntime 'org.junit.platform:junit-platform-launcher:1.3.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Main-Class': 'com.igdb.IGDBWrapper'
    }
    from configurations.compileClasspath.filter{
        it.exists() && it.toString().contains("protobuf-java")
    }.collect { it.isDirectory() ? it : zipTree(it) }
}

protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = "com.google.protobuf:protoc:3.0.0"
    }
    /* Change the path to the generated files */
    generatedFilesBaseDir = "$projectDir/src"

}

clean {
    delete "$projectDir/src/java"
}

sourceSets {
    main {
        proto {
            /* The default location for the .proto files is src/main/proto */
            srcDir "$projectDir/src/main/resources"
        }
    }
    test {
        proto {
            // In addition to the default 'src/test/proto'
            srcDir 'src/test/kotlin/'
        }
    }
}
